// Filter data for specific zone
constraint ZoneIDRangeConstraint oftype RangeConstraint { lowerBound: 1925; upperBound: 1925; }
valuetype ZoneID oftype integer { constraints: [ ZoneIDRangeConstraint ]; }

// Data validation for geographic coordinates
constraint GeoCoordinatesRangeConstraint oftype RangeConstraint { lowerBound: -90; upperBound: 90; }
valuetype GeoCoordinates oftype decimal { constraints: [ GeoCoordinatesRangeConstraint ]; }


// Data validation for stop name to include German umlauts
constraint StopNameConstraint oftype RegexConstraint { regex: /[a-zA-ZäöüÄÖÜß]+/; }
valuetype StopName oftype text { constraints: [ StopNameConstraint ]; }

pipeline GTFSPipeline {

    // Build a pipe of all the GTFSPipeline's blocks
    GTFSExtractor
        -> GTFSArchiveInterpreter
        -> GTFSFilePicker
        -> GTFSTextFileInterpreter
        -> GTFSCSVInterpreter
        -> GTFSTableInterpreter
        -> GTFSLoader;

    // Extracts zip file from the desired url
    block GTFSExtractor oftype HttpExtractor { url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip"; }

    // Unzip extracted data
    block GTFSArchiveInterpreter oftype ArchiveInterpreter { archiveType: "zip"; }

    // Use the desired file in the unzip directory
    block GTFSFilePicker oftype FilePicker { path: "/stops.txt"; }

    // Converts from File type to TextFile type
    block GTFSTextFileInterpreter oftype TextFileInterpreter { encoding: "utf8"; }

    // Converts from type TextFile to CSV
    block GTFSCSVInterpreter oftype CSVInterpreter {
        delimiter: ",";
        enclosing: '"';
    }

    // Converts from CSV to Table and dropped unnecessary columns
    block GTFSTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype GeoCoordinates,
            "stop_lon" oftype GeoCoordinates,
            "zone_id" oftype ZoneID
        ];
    }

    // Load data into sqlite database
    block GTFSLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }
}
