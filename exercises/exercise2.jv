pipeline TreePipeline {

    // Pipeline Workflow
    TreeExtractor 
        -> TreeTextFileInterpreter
        -> TreeCSVInterpreter
        -> ColumnRemover
        -> DataCleaner
        -> TreeDatabaseLoader;
    
    //  CSV file from the URL
    block TreeExtractor oftype HttpExtractor {
        url: 'https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv';
    }

    // Interpret data 
    block TreeTextFileInterpreter oftype TextFileInterpreter { }

    
    block TreeCSVInterpreter oftype CSVInterpreter {
        delimiter: ';';
    }

    // Remove unnecessary columns
    block ColumnRemover oftype ColumnDeleter {
        delete: [column E];
    }

    //  data constraints
    block DataCleaner oftype TableInterpreter {
        header: true;
        columns: [
            'lfd_nr' oftype integer,
            'stadtteil' oftype VogelsangConstraint,
            'standort' oftype text,
            'baumart_botanisch' oftype text,
            'id' oftype GeoConstraint,
            'baumfamilie' oftype text
        ];
    }

    //  SQLite database
    block TreeDatabaseLoader oftype SQLiteLoader {
        table: 'trees';
        file: './trees.sqlite';
    }

    
    constraint VogelsangRegex oftype RegexConstraint {
        regex: /^Vogelsang/;
    }

    valuetype VogelsangConstraint oftype text {
        constraints: [
            VogelsangRegex,
        ];
    }

    
    constraint GeoRegex oftype RegexConstraint {
        regex: /([0-9]{1,3}\.[0-9]+),\s*([0-9]{1,3}\.[0-9]+)/;
    }

    valuetype GeoConstraint oftype text {
        constraints: [
            GeoRegex,
        ];
    }
}
